*** Settings ***
Force Tags    Sprint-2    Regression
Resource      resource.txt


***Test Cases***

Show vacation for employee
    Given current date is 2011-11-24
    and employee "Janne" with start date 2011-01-01 exists in the system
    When "Janne" is selected
    Then 6 days is shown for year 2011


*** Keywords ***

Current date is ${date}
    Set Environment Variable    VACALC_DATE    ${date}

Employee "${name}" with start date ${date} exists in the system
    Start Vacalc with No Employees
    Add Employee    ${name}    ${date}

"${employee}" is selected
    Select Employee    ${employee}

${days:\d+} days is shown for year ${year:\d+}
    Correct Vacation Should Be Shown    ${days}    ${year}
